#!/bin/zsh
zmodload -Fa zsh/files b:zf_mv
zmodload -Fa zsh/parameter p:commands p:dirstack p:functions

.autocomplete.recent-dirs.precmd() {
  if (( precmd_functions[(I)_zshz_precmd] )) &&
      zstyle -T ':autocomplete:' recent-dirs 'zsh-z'; then
    _autocomplete.recent_dirs() {
      reply=( ${(f)"$( zshz --complete -l $1 2> /dev/null )"} )
    }

  elif (( chpwd_functions[(I)__zoxide_hook] )) &&
      zstyle -T ':autocomplete:' recent-dirs 'zoxide'; then
    _autocomplete.recent_dirs() {
      reply=( ${(f)"$( zoxide query --list $1 2> /dev/null )"} )
    }

  elif (( precmd_functions[(I)_zlua_precmd] )) &&
      zstyle -T ':autocomplete:' recent-dirs 'z.lua'; then
    _autocomplete.recent_dirs() {
      reply=( ${${(f)"$( _zlua --complete $1 2> /dev/null )"}##<->[[:space:]]##} )
    }

  elif (( precmd_functions[(I)_z_precmd] )) &&
      zstyle -T ':autocomplete:' recent-dirs 'z.sh'; then
    _autocomplete.recent_dirs() {
      reply=( ${${(fOa)"$( _z -l $1 2>&1 )"}##(common:|<->)[[:space:]]##} )
    }

  elif (( chpwd_functions[(I)autojump_chpwd] )) &&
      zstyle -T ':autocomplete:' recent-dirs 'autojump'; then
    _autocomplete.recent_dirs() {
      reply=( ${${(f)"$( autojump --complete $1 2> /dev/null )"}##${1}__<->__} )
    }

  elif (( preexec_functions[(I)_fasd_preexec] )) &&
      zstyle -T ':autocomplete:' recent-dirs 'fasd'; then
    _autocomplete.recent_dirs() {
      reply=( ${(f)"$( fasd -dlR $1 2> /dev/null )"} )
    }

  elif builtin autoload -Uz chpwd_recent_dirs chpwd_recent_filehandler &&
      zstyle -T ':autocomplete:' recent-dirs 'cdr'; then
    setopt autopushd pushdignoredups

    if ! zstyle -s :chpwd: recent-dirs-file _; then
      private old=${ZDOTDIR:-$HOME}/.chpwd-recent-dirs
      private new=${XDG_DATA_HOME:-$HOME/.local/share}/zsh/chpwd-recent-dirs
      zstyle ':chpwd:*' recent-dirs-file $new
      [[ -e $old && ! -e $new ]] &&
          zf_mv "$old" "$new"
    fi
    zstyle -s :chpwd: recent-dirs-max _ ||
        zstyle ':chpwd:*' recent-dirs-max 0

    if ! (( $#dirstack[@] )); then
      local -aU reply=()
      chpwd_recent_filehandler
      dirstack=( ${^reply[@]:#$PWD}(N-/) )
    fi

    _autocomplete.recent_dirs.save() {
      chpwd_recent_filehandler $PWD $dirstack[@]
    }
    add-zsh-hook chpwd _autocomplete.recent_dirs.save

    _autocomplete.recent_dirs() {
      private -a dirs=( ${^dirstack[@]:#$PWD(|/[^/]#)}(N) )

      private ancestor=$PWD:h
      while [[ $ancestor != / ]]; do
        dirs=( ${dirs[@]:#$ancestor} )
        ancestor=$ancestor:h
      done

      private -a displ=( "${(@D)dirs}" )
      private -A displ_to_dirs=( "${(@)displ:^dirs}" )
      _autocomplete.sort_by_length "$displ[@]"

      private -i MBEGIN= MEND=
      private MATCH=
      reply=( ${reply[@]:/(#m)*/${displ_to_dirs[$MATCH]}} )

      (( $#reply[@] ))
    }
  fi

  if (( preexec_functions[(I)_fasd_preexec] )) &&
      zstyle -T ':autocomplete:' recent-files 'fasd'; then
    _autocomplete.recent_files() {
      reply=( $( fasd -flR $1 2> /dev/null ) )
    }
  fi
}
